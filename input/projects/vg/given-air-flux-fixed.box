// vg/given-air-flux-fixed.box
Simulation {
  .steps = 60
  Calendar calendar {
    .timeStep = 5
    .timeUnit = "m"
  }
  Box drivingVariables {
    Box outdoors {
      +temperature = 15
      +windSpeed = 4 
    }
    Box indoors {
      +rh = 90 
    }
    Box setpoints {
      +maximumRh = 80
    }
  }
  AirFluxGiven givenAirFlux {
    AirFluxInfiltration infiltration {
      .leakage = 1
      .windSpeed = outdoors[windSpeed]
    }
    Accumulator crackVentilation {
      .change = ./controller[controlVariable]
      PidController controller {
        .desiredValue = ./desiredVentilation[signal]
        .sensedValue = ..[value]
        .Kprop = 0.02 
        ProportionalSignal desiredVentilation {
          .input = indoors[rh]
          .threshold = setpoints[maximumRh]
          .thresholdBand = 10
          .increasingSignal = TRUE
          .maxSignal = ./maxVentilation[signal]
          .minSignal = 0
          ProportionalSignal maxVentilation {
            .input = outdoors[temperature]
            .threshold = -5
            .thresholdBand = 1
            .increasingSignal = TRUE
            .maxSignal = 0.5
            .minSignal = 0
          }
        }
      }
    }
    AirFluxGravitation gravitation {
      .screensState = actuators/screens[value]
      .indoorsTemperature = 22
      .outdoorsTemperature = outdoors[temperature]
      .indoorsVolume = 6400
      .groundArea = 1000
    }
  }
  Box actuators {
    Accumulator screens {
      .change = ./controller[controlVariable]
      .initial = 1
      PidController controller {
        .desiredValue = 0
        .sensedValue = ..[value]
        .Kprop = 0.05 
      }
    }
  }
  OutputR {
    PageR {
      .xAxis = calendar[time]
      PlotR {
        .layout = "merged"
        .ports = (givenAirFlux[value] givenAirFlux/*[value])
        .ggplot = "geom_line() + geom_point() + 
                    xlab('Time (h)') +
                    ylab(expression(paste('Ventilation ', (m^3/m^3/h))))"
      }
      PlotR {
        .ports = screens[value]
        .ggplot = "geom_line() + geom_point() +
                    xlab('Time (h)') +
                    ylab('Screens in effect')"
      }
    }
  }
}