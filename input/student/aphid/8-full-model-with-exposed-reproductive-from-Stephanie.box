Simulation sim {
  .steps=273 // end of september

  Box param {
    +juvenileApterousDuration = 178.5
    +juvenileAlateDuration = 203.6
    +adultDuration = 300
    +k = 25
    +fecundityDuration = 100
    +fecundity_k = 1
  }

  Calendar calendar {
    .timeStep=1
    .timeUnit="d" 
    .initialDateTime = 1/1/2017
  }
  
  Records weather {
    .fileName= "weather/Rygge_2017.txt"
  } 
  
  //
  // Wheat
  //
  
  Box wheat{
    CropGrowthStage cropGrowthStage {
      .valueAtStart=  20
      .dayDegrees = ./time[total]
      .dayDegreesHalfways = 720
      CropIsGrowing isGrowing { 
        .temperature = weather[Tavg] 
      }
      DayDegrees time{
        .T = weather[Tavg]
        .T0 = 0
        .isTicking = ../isGrowing[value]
      }
    }
  }
  
  //
  // Aphid
  //
  
  Maker maker {
    .names = ("withoutFungus" "withFungus")
    Box aphid {
      DayDegrees time{
        .T = weather[Tavg]
        .T0 = 3 
        .Topt = 18 
        .Tmax = 30 
      }  
      DayDegrees fungusTime {
        .T = weather[Tavg]
        .T0 = 2 
        .Topt = 18 
        .Tmax = 30 
      }
      Box density {
        +nymphs|sum = ../*/nymph/*[content] 
        +adults|sum = ../*/adult/*[content] 
        +total|sum  = ../*/*/*[content]     
      }
      AphidImmigration immigration {
        .cropGrowthStage = cropGrowthStage[value]
        .immigrationRate = 0.02 // 750 tillers on average in 1 m2 in Norway
        .propExposedImmigrants = 0.5
        .k = param[k]
      }
      AphidNetReproduction netReproduction {
        .Tmin = 3 
        .Topt = 16.1 
        .Tmax = 30
        .R0opt = 51.6
        .temperature = weather[Tavg]
        .cropGrowthStage = cropGrowthStage[value]
        .aphidDensity = ../density[total]
        .aphidDensityThreshold = 40
        .exposureCost = 0.3
      }
      AphidOffspring offspring {
        .offspringFromSusceptible|sum = ../susceptible/adult/*/fecundity[outflow] 
        .offspringFromExposedApterous|sum = ../exposed/adult/apterous/fecundity[outflow] // why not exposed/adult/*/fecundity[outflow] ?
        .offspringFromExposedAlate|sum = ../exposed/adult/alate/fecundity[outflow] 
        .aphidDensity = ../density[total]
        .cropGrowthStage = cropGrowthStage[value]
      }
      AphidJuvenileSurvival survival{
        .cropGrowthStage = cropGrowthStage[value]
        .temperature = weather[Tavg]
      }
      Box susceptible {
        Box nymph {
          Stage apterous {
            .inflow = .../offspring[apterous]
            .timeStep = .../time[step]
            .k = param[k]
            .duration = param[juvenileApterousDuration]
            .growthFactor = .../survival[value]
            .phaseOutflowProportion = .../infectious/infectionRate[value]
          }
          Stage alate {
            .inflow = .../offspring[alate]
            .timeStep = .../time[step]
            .k = param[k]
            .duration = param[juvenileAlateDuration]
            .growthFactor = .../survival[value]
            .phaseOutflowProportion = .../infectious/infectionRate[value]
          }
        }
        Box adult {
          Stage apterous {
            .inflow = .../nymph/apterous[outflow]
            .timeStep = .../time[step]
            .duration = param[adultDuration]
            .k = param[k]
            .phaseOutflowProportion = .../infectious/infectionRate[value]
            Stage fecundity {
              .inflow = .../nymph/apterous[outflow]
              .timeStep = .../time[step]
              .duration = param[fecundityDuration]
              .k = param[fecundity_k]
              .growthFactor = .../netReproduction[apterousSusceptible] // change the name of the output of netReproduction to make it clearer
              .phaseOutflowProportion = .../infectious/infectionRate[value]
            }
          }
          Stage alate {
            .inflow = .../immigration[susceptible]
            .timeStep = .../time[step]
            .duration = param[adultDuration]
            .k = param[k]
            .phaseOutflowProportion = .../infectious/infectionRate[value]
            Stage fecundity {
              .inflow = .../immigration[susceptible]
              .timeStep = .../time[step]
              .duration = param[fecundityDuration]
              .k = param[fecundity_k]
              .growthFactor = .../netReproduction[alateSusceptible] // idem
              .phaseOutflowProportion = .../infectious/infectionRate[value]
            }
          }
        }
      } // susceptible
      Box exposed {
        +lethalTimeNymph = 73
        +lethalTimeAdult = 72
        Box nymph {
          StageAndPhase apterous {
            .timeStep = .../time[step]
            .k = param[k]
            .duration = param[juvenileApterousDuration]
            .growthFactor = .../survival[value]
            .phaseInflow = .../susceptible/nymph/apterous[phaseOutflow]
            .phaseTimeStep = .../fungusTime[step]
            .phaseK = param[k]
            .phaseDuration = .../exposed[lethalTimeNymph]
          }
          StageAndPhase alate {
            .timeStep = .../time[step]
            .k = param[k]
            .duration = param[juvenileAlateDuration]
            .growthFactor = .../survival[value]
            .phaseInflow = .../susceptible/nymph/alate[phaseOutflow]
            .phaseTimeStep = .../fungusTime[step]
            .phaseK = param[k]
            .phaseDuration = .../exposed[lethalTimeNymph]
          }
        } // nymph
        Box adult {
          StageAndPhase apterous {
            .inflow = .../nymph/apterous[outflow] // These are sterile.
            .timeStep = .../time[step]
            .duration = param[adultDuration]
            .k = param[k]
            .phaseInflow = .../susceptible/adult/apterous[phaseOutflow]
            .phaseTimeStep = .../fungusTime[step]
            .phaseK = param[k]
            .phaseDuration = .../exposed[lethalTimeAdult]
            StageAndPhase fecundity {
              .timeStep = .../time[step]
              .duration = param[fecundityDuration]
              .k = param[fecundity_k]
              .growthFactor = .../netReproduction[apterousExposed] 
              .phaseInflow = .../susceptible/adult/apterous/fecundity[phaseOutflow]
              .phaseTimeStep = ..[phaseTimeStep]
              .phaseK = param[k]
              .phaseDuration = ..[phaseDuration]
            }
          }
          StageAndPhase alate {
            .inflow = .../immigration[exposed]
            .timeStep = .../time[step]
            .duration = param[adultDuration]
            .k = param[k]
            .phaseInflow = .../susceptible/adult/alate[phaseOutflow]
            .phaseTimeStep = .../fungusTime[step]
            .phaseK = param[k]
            .phaseDuration = .../exposed[lethalTimeAdult]
            StageAndPhase fecundity {
			  .inflow = .../immigration[exposed] // These ones reproduce as well
              .timeStep = .../time[step]
              .duration = param[fecundityDuration]
              .k = param[fecundity_k]
              .growthFactor = .../netReproduction[alateExposed] 
              .phaseInflow = .../susceptible/adult/alate/fecundity[phaseOutflow] 
              .phaseTimeStep = ..[phaseTimeStep]
              .phaseK = param[k]
              .phaseDuration = ..[phaseDuration]
            }
          }
        } // adult
        CadaverConversion succumbed {
          .succumbedApterousNymphs = .../nymph/apterous[phaseOutflow]
          .succumbedAlateNymphs    = .../nymph/alate[phaseOutflow]  
          .succumbedApterousAdult  = .../adult/apterous[phaseOutflow]
          .succumbedAlateAdults    = .../adult/alate[phaseOutflow]   
        }
      } // exposed
      Box infectious {
        OnOff isSporulating {
          .x = weather[RHmax]
          .xOn = 95
          .xOff = 100
        }
        CadaverTime time {
          .isSporulating = ../isSporulating[isOn]
          .timeStep = .../fungusTime[step]
          .rhAccelaration = 2.33
        }
        Stage cadavers {
          .inflow = .../exposed/succumbed[cadavers]
          .timeStep = .../time[step]
          .duration = 112
          .k = param[k]
        }
        InfectionRate infectionRate {
          .isSporulating = ../isSporulating[isOn]
          .cadavers = ../cadavers[content]
          .transmissionEfficiency = 0.1728
        }
      } // infectious
      Box diagnostics {
        Accumulator aphidDays {
          .change = ../../density[total]
        }
        Accumulator cadaverDays {
          .change = ../../infectious/cadavers[content]
        }
        ThresholdNorway thresholdNorway {
          .aphids = ../../density[total]
          .cropGrowthStage = cropGrowthStage[value]
        }
        ThresholdSweden thresholdSweden {
          .aphids = ../../density[total]
          .cropGrowthStage = cropGrowthStage[value]
       }
        AphidIndex aphidIndex {
          .nymphs = ../../density[nymphs]
          .adults = ../../density[adults]
        }
        aphid::Yield yield {
          .aphidIndex = ../aphidIndex[value]
          .cropGrowthStage = cropGrowthStage[value]
        }
      } // diagnostics
    } // aphid
  } // maker
  
  Biocontrol biocontrol {
    .aphidDaysUncontrolled = withoutFungus/diagnostics/aphidDays[value] 
    .aphidDaysControlled = withFungus/diagnostics/aphidDays[value] 
    .yieldUncontrolled = withoutFungus/diagnostics/yield[yield] 
    .yieldControlled = withFungus/diagnostics/yield[yield] 
  }
  
  //
  // Output
  //
  
  OutputR {
    +ggplot = "geom_line(size=1.1) + scale_x_datetime(breaks = date_breaks('months'), labels = date_format('%b'))"
    PageR {
      .xAxis = calendar[date] 
      .title = "Aphids"
      .ncol = 4
      PlotR {
        +cropGrowthStage = cropGrowthStage[value]
        .ports = (.[cropGrowthStage] netReproduction[apterous] netReproduction[alate]
                   offspring[apterous] offspring[alate] offspring[alateProportion])
        .ggplot = ../..[ggplot] 
      }
      PlotR {
        .ports = (immigration[susceptible] susceptible/*/*[content] density[total])
        .ggplot = ../..[ggplot] 
      }
      PlotR {
        +withFungusImmigrationExposed|sum = withFungus/immigration[exposed]
        +withoutFungusImmigrationExposed|sum = withoutFungus/immigration[exposed]
        .ports = (.[withFungusImmigrationExposed] .[withoutFungusImmigrationExposed] exposed/*/*[content] cadavers[content])
        .ggplot = ../..[ggplot] 
      }
      PlotR {
        .ports = (infectious/infectionRate[value] aphidDays[value] cadaverDays[value]
                   thresholdNorway[exceeded] thresholdSweden[exceeded] aphidIndex[value] yield[yield])
        .ggplot = ../..[ggplot] 
      }
    }
    PageR {
      .xAxis = calendar[date] 
      .title = "Biocontrol effect"
      PlotR {
        .ports = (aphidDays[value] cadaverDays[value] yield[yield] biocontrol[aphidDaysImprovement] biocontrol[yieldImprovement])
        .ggplot = ../..[ggplot] 
      }
    }
    PageR {
      .xAxis = calendar[date] 
      .title = "Now two sources of offspring"

      PlotR {
        .ports = (withFungus/offspring[offspringFromSusceptible] 
                  withFungus/offspring[offspringFromExposedApterous] 
                  withFungus/offspring[offspringFromExposedAlate] )
        .ggplot = ../..[ggplot] 
      }
    }
  }

}