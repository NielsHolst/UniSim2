Simulation sim {
  .steps=273 // end of september

  Calendar calendar {
    .timeStep=1
    .timeUnit="d" 
    .initialDateTime = 1/1/2017
  }
  
  Records weather {
    .fileName= "weather/Rygge_2017.txt"
  } 
  
  //
  // Wheat
  //
  
  Box wheat{
    CropGrowthStage cropGrowthStage {
      .valueAtStart=  20
      .dayDegrees = ./time[total]
      .dayDegreesHalfways = 720
      CropIsGrowing isGrowing {
        .temperature = weather[Tavg] 
      }
      DayDegrees time{
        .T = weather[Tavg]
        .T0 = 0
        .isTicking = ../isGrowing[value]
      }
    }
  }

  //
  // Aphid
  //
  
  Box aphid {
    Box common {
      +juvenileApterousDuration = 178.5
      +juvenileAlateDuration = 203.6
      +adultDuration = 300
      +k = 25
    }
    DayDegrees time{
      .T = weather[Tavg]
      .T0 = 3 
      .Topt = 18 
      .Tmax = 30 
    }  
    DayDegrees fungusTime {
      .T = weather[Tavg]
      .T0 = 2 
      .Topt = 18 
      .Tmax = 30 
    }
    Box density {
      +nymphs|sum = ../*/nymph/*[content] 
      +adults|sum = ../*/adult/*[content] 
      +total|sum  = ../*/*/*[content]     
    }
    AphidImmigration immigration {
      .cropGrowthStage = cropGrowthStage[value]
      .immigrationRate = 0.015
      .propExposedImmigrants = 0.5
      .k = .../common[k]
    }
    AphidNetReproduction netReproduction {
      .Tmin = 3 
      .Topt = 16.1 
      .Tmax = 30
      .R0opt = 51.6
      .temperature = weather[Tavg]
      .cropGrowthStage = cropGrowthStage[value]
      .aphidDensity = ../density[total]
      .aphidDensityThreshold = 40
    }
    AphidOffspring offspring {
      .aphidOffspring|sum = ../susceptible/adult/*/fecundity[outflow] // only susceptible reproduce
      .aphidDensity = ../density[total]
      .cropGrowthStage = cropGrowthStage[value]
    }
    AphidJuvenileSurvival survival{
      .cropGrowthStage = cropGrowthStage[value]
      .temperature = weather[Tavg]
    }
    Box susceptible {
      Box nymph {
        Stage apterous {
          .inflow = .../offspring[apterous]
          .timeStep = .../time[step]
          .k = .../common[k]
          .duration = .../common[juvenileApterousDuration]
          .growthFactor = .../survival[value]
        }
        Stage alate {
          .inflow = .../offspring[alate]
          .timeStep = .../time[step]
          .k = .../common[k]
          .duration = .../common[juvenileAlateDuration]
          .growthFactor = .../survival[value]
        }
      }
      Box adult {
        +fecundityDuration = 100
        +fecundity_k = 1
        Stage apterous {
          .inflow = .../nymph/apterous[outflow]
          .timeStep = .../time[step]
          .duration = .../common[adultDuration]
          .k = .../common[k]
          Stage fecundity {
            .inflow = .../nymph/apterous[outflow]
            .timeStep = .../time[step]
            .duration = ...[fecundityDuration]
            .k = ...[fecundity_k]
            .growthFactor = .../netReproduction[apterous]
          }
        }
        Stage alate {
          .inflow = .../immigration[susceptible]
          .timeStep = .../time[step]
          .duration = .../common[adultDuration]
          .k = .../common[k]
          Stage fecundity {
            .inflow = .../immigration[susceptible]
            .timeStep = .../time[step]
            .duration = ...[fecundityDuration]
            .k = ...[fecundity_k]
            .growthFactor = .../netReproduction[alate]
          }
        }
      }
    } // susceptible
    Box exposed {
      +lethalTimeNymph = 73
      +lethalTimeAdult = 72
      Box nymph {
        StageAndPhase apterous {
          .timeStep = .../time[step]
          .k = .../common[k]
          .duration = .../common[juvenileApterousDuration]
          .growthFactor = .../survival[value]
          .phaseInflow = .../susceptible/nymph/apterous[phaseOutflow]
          .phaseTimeStep = .../fungusTime[step]
          .phaseK = .../common[k]
          .phaseDuration = .../exposed[lethalTimeNymph]
        }
        StageAndPhase alate {
          .timeStep = .../time[step]
          .k = .../common[k]
          .duration = .../common[juvenileAlateDuration]
          .growthFactor = .../survival[value]
          .phaseInflow = .../susceptible/nymph/alate[phaseOutflow]
          .phaseTimeStep = .../fungusTime[step]
          .phaseK = .../common[k]
          .phaseDuration = .../exposed[lethalTimeNymph]
        }
      } // nymph
      Box adult {
        StageAndPhase apterous {
          .inflow = .../nymph/apterous[outflow]
          .timeStep = .../time[step]
          .duration = .../common[adultDuration]
          .k = .../common[k]
          .phaseInflow = .../susceptible/adult/apterous[phaseOutflow]
          .phaseTimeStep = .../fungusTime[step]
          .phaseK = .../common[k]
          .phaseDuration = .../exposed[lethalTimeAdult]
        }
        StageAndPhase alate {
          .inflow = .../immigration[exposed]
          .timeStep = .../time[step]
          .duration = .../common[adultDuration]
          .k = .../common[k]
          .phaseInflow = .../susceptible/adult/alate[phaseOutflow]
          .phaseTimeStep = .../fungusTime[step]
          .phaseK = .../common[k]
          .phaseDuration = .../exposed[lethalTimeAdult]
        }
      } // adult
      CadaverConversion succumbed {
        .succumbedApterousNymphs = .../nymph/apterous[phaseOutflow]
        .succumbedAlateNymphs    = .../nymph/alate[phaseOutflow]  
        .succumbedApterousAdult  = .../adult/apterous[phaseOutflow]
        .succumbedAlateAdults    = .../adult/alate[phaseOutflow]   
      }
    } // exposed
    Box infectious {
      OnOff isSporulating {
        .x = weather[RHmax]
        .xOn = 95
        .xOff = 100
      }
      CadaverTime time {
        .isSporulating = ../isSporulating[isOn]
        .timeStep = .../fungusTime[step]
        .rhAccelaration = 2.33
      }
      Stage cadavers {
        .inflow = .../exposed/succumbed[cadavers]
        .timeStep = .../time[step]
        .duration = 112
        .k = .../common[k]
      }
      InfectionRate infectionRate {
        .isSporulating = ../isSporulating[isOn]
        .cadavers = ../cadavers[content]
        .transmissionEfficiency = 0.1728
      }
    } // infectious
    Box diagnostics {
      Accumulator aphidDays {
        .change = ../../density[total]
      }
      Accumulator cadaverDays {
        .change = ../../infectious/cadavers[content]
      }
      ThresholdNorway thresholdNorway {
        .aphids = ../../density[total]
        .cropGrowthStage = cropGrowthStage[value]
      }
      ThresholdSweden thresholdSweden {
        .aphids = ../../density[total]
        .cropGrowthStage = cropGrowthStage[value]
     }
      AphidIndex aphidIndex {
        .nymphs = ../../density[nymphs]
        .adults = ../../density[adults]
      }
      aphid::Yield yield {
        .aphidIndex = ../aphidIndex[value]
        .cropGrowthStage = cropGrowthStage[value]
      }
    } // diagnostics
  } // aphid
  
  OutputR {
    +ggplot = "geom_line(size=1.1) + scale_x_datetime(breaks = date_breaks('months'), labels = date_format('%b'))"
    PageR {
      .xAxis = calendar[date] 
      .title = "Aphids"
      .ncol = 4
      PlotR {
        +cropGrowthStage = cropGrowthStage[value]
        .ports = (.[cropGrowthStage] netReproduction[apterous] netReproduction[alate]
                   offspring[apterous] offspring[alate] offspring[alateProportion])
        .ggplot = ../..[ggplot] 
      }
      PlotR {
        +susceptibleImmigration = immigration[susceptible]
        .ports = (.[immigration] susceptible/*/*[content] density[total])
        .ggplot = ../..[ggplot] 
      }
      PlotR {
        +exposedImmigration = immigration[exposed]
        .ports = (.[immigration] exposed/*/*[content] cadavers[content])
        .ggplot = ../..[ggplot] 
      }
      PlotR {
        .ports = (infectious/infectionRate[value] aphidDays[value] cadaverDays[value]
                   thresholdNorway[exceeded] thresholdSweden[exceeded] aphidIndex[value] yield[yield])
        .ggplot = ../..[ggplot] 
      }
    }
  }
}