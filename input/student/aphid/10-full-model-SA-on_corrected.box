Simulation sim {
  .iterations = 10240 //20480
  .steps =  273 // end of september
  .silent = TRUE
  SelectFile weatherFiles {
    .folder = "weather"
    .filter = "*.txt"
    .showFileNames = FALSE
  }
  Box random {
    RandomiserSobolSequence randomiser {
      .useFixed = FALSE
      .doSensitivityAnalysis = TRUE
      .bootstrapSize = 10000
    }
    RandomUniformInt fileNumber {
      .min = 1
      .fixed = 1
      .max = weatherFiles[numFiles]
    }
    RandomNormal cropAtStart {
      .min = 10
      .fixed = 20
      .max = 30 // 27
    }
    RandomNormal sporulationOn {
      .min = 80 //90
      .fixed = 92.5
      .max = 99 // 95?
    }
    RandomNormal cadaverDuration {
      .min = 32 // 54
      .fixed = 112
      .max = 112 //130
    }
    RandomNormal propExposedImmigrants {
      .min = 0.1
      .fixed = 0.5
      .max = 0.7
    }
    RandomNormal transmissionEfficiency {
      .min = 0.05
      .fixed = 0.1728
      .max = 0.5 //0.1728
    }
    RandomNormal lethalTimeNymph {
      .min = 50 // 50
      .fixed = 73
      .max = 115 // 180
    }
    RandomNormal timeAcceleration {
      .min=1
      .fixed=2.33
      .max=3.5
    }
    RandomNormal cropHalfways {
      .useFixed = TRUE
      .min = 750 // 747
      .fixed = 800
      .max = 850 // 843
    }
    RandomNormal lethalTimeAdult {
      .useFixed = TRUE
      .min = 50 // 60
      .fixed = 72
      .max = 115 // 180
    }
    RandomNormal immunityCost{
      .useFixed = TRUE
      .min = 0
      .fixed = 0.2
      .max = 0.4
    }
    RandomUniformInt k {
      .useFixed = TRUE
      .min = 15
      .fixed = 15
      .max = 30
    }
  }
  Box param {
    +juvenileApterousDuration = 172 // 178.5
    +juvenileAlateDuration = 195 //203.6
    +adultDuration = 300
    +fecundityDuration = 100
    +fecundity_k = 1
  }

  Calendar calendar {
    .timeStep=1
    .timeUnit="d" 
    .initialDateTime = 1/1/2017 // maybe this will change with weather file
  }

  Records weather {
    .fileName =  ./files[fileNamePath]   //"weather/Rygge_2017.txt"
    .overrideCalendarYear = TRUE
    SelectFile files {
      .folder = "weather"
      .filter = "*.txt"
      .selectFileNumber = random/fileNumber[value]
      .showFileNames = FALSE
    }
  } 
  
  //
  // Wheat
  //
  
  Box wheat{
    CropGrowthStage cropGrowthStage {
      .valueAtStart=  random/cropAtStart[value]
      .dayDegrees = ./time[total]
      .dayDegreesHalfways = random/cropHalfways[value] //720
      CropIsGrowing isGrowing {
        .temperature = weather[Tavg] 
      }
      DayDegrees time{
        .T = weather[Tavg]
        .T0 = 0
        .isTicking = ../isGrowing[value]
      }
    }
  }
  
  //
  // Aphid
  //
  
  Maker maker {
    .names = ("withoutFungus" "withFungus")
    Box aphid {
      DayDegrees time{
        .T = weather[Tavg]
        .T0 = 3 
        .Topt = 18 
        .Tmax = 30 
      }  
      DayDegrees fungusTime {
        .T = weather[Tavg]
        .T0 = 2 
        .Topt = 18 
        .Tmax = 30 
      }
      Box density {
        +nymphs|sum = ../*/nymph/*[content] 
        +adults|sum = ../*/adult/*[content] 
        +total|sum  = ../*/*/*[content]     
      }
      AphidImmigration immigration {
        .cropGrowthStage = cropGrowthStage[value]
        .immigrationRate = 0.015
        .propExposedImmigrants = random/propExposedImmigrants[value]
        .k = random/k[value]
      }
      AphidNetReproduction netReproduction {
        .Tmin = 3 
        .Topt = 16.1 
        .Tmax = 30
        .R0opt = 51.6
        .temperature = weather[Tavg]
        .cropGrowthStage = cropGrowthStage[value]
        .aphidDensity = ../density[total]
        .aphidDensityThreshold = 40 
        .exposureCost = random/immunityCost[value] //0.3
      }
      AphidOffspring offspring {
        .offspringFromSusceptible|sum = ../susceptible/adult/*/fecundity[outflow] 
        .offspringFromExposedApterous|sum = ../exposed/adult/apterous/fecundity[outflow] 
        .offspringFromExposedAlate|sum = ../exposed/adult/alate/fecundity[outflow] 
        .aphidDensity = ../density[total]
        .cropGrowthStage = cropGrowthStage[value]
      }
      AphidJuvenileSurvival survival{
        .cropGrowthStage = cropGrowthStage[value]
        .temperature = weather[Tavg]
      }
      Box susceptible {
        Box nymph {
          Stage apterous {
            .inflow = .../offspring[apterous]
            .timeStep = .../time[step]
            .k = random/k[value]
            .duration = param[juvenileApterousDuration]
            .growthFactor = .../survival[value]
            .phaseOutflowProportion = .../infectious/infectionRate[value]
          }
          Stage alate {
            .inflow = .../offspring[alate]
            .timeStep = .../time[step]
            .k = random/k[value]
            .duration = param[juvenileAlateDuration]
            .growthFactor = .../survival[value]
            .phaseOutflowProportion = .../infectious/infectionRate[value]
          }
        }
        Box adult {
          Stage apterous {
            .inflow = .../nymph/apterous[outflow]
            .timeStep = .../time[step]
            .duration = param[adultDuration]
            .k = random/k[value]
            .phaseOutflowProportion = .../infectious/infectionRate[value]
            Stage fecundity {
              .inflow = .../nymph/apterous[outflow]
              .timeStep = .../time[step]
              .duration = param[fecundityDuration]
              .k = param[fecundity_k]
              .growthFactor = .../netReproduction[apterous]
              .phaseOutflowProportion = .../infectious/infectionRate[value]
            }
          }
          Stage alate {
            .inflow = .../immigration[susceptible]
            .timeStep = .../time[step]
            .duration = param[adultDuration]
            .k = random/k[value]
            .phaseOutflowProportion = .../infectious/infectionRate[value]
            Stage fecundity {
              .inflow = .../immigration[susceptible]
              .timeStep = .../time[step]
              .duration = param[fecundityDuration]
              .k = param[fecundity_k]
              .growthFactor = .../netReproduction[alate]
              .phaseOutflowProportion = .../infectious/infectionRate[value]
            }
          }
        }
      } // susceptible
      Box exposed {
        // +lethalTimeNymph = 73 
        // +lethalTimeAdult = 72 
        Box nymph {
          StageAndPhase apterous {
            .timeStep = .../time[step]
            .k = random/k[value]
            .duration = param[juvenileApterousDuration]
            .growthFactor = .../survival[value]
            .phaseInflow = .../susceptible/nymph/apterous[phaseOutflow]
            .phaseTimeStep = .../fungusTime[step]
            .phaseK = random/k[value]
            .phaseDuration = random/lethalTimeNymph[value]
          }
          StageAndPhase alate {
            .timeStep = .../time[step]
            .k = random/k[value]
            .duration = param[juvenileAlateDuration]
            .growthFactor = .../survival[value]
            .phaseInflow = .../susceptible/nymph/alate[phaseOutflow]
            .phaseTimeStep = .../fungusTime[step]
            .phaseK = random/k[value]
            .phaseDuration = random/lethalTimeNymph[value]
          }
        } // nymph
        Box adult {
          StageAndPhase apterous {
            .inflow = .../nymph/apterous[outflow]
            .timeStep = .../time[step]
            .duration = param[adultDuration]
            .k = random/k[value]
            .phaseInflow = .../susceptible/adult/apterous[phaseOutflow]
            .phaseTimeStep = .../fungusTime[step]
            .phaseK = random/k[value]
            .phaseDuration = random/lethalTimeAdult[value]
            StageAndPhase fecundity {
              .timeStep = .../time[step]
              .duration = param[fecundityDuration]
              .k = param[fecundity_k]
              .growthFactor = .../netReproduction[apterousExposed] // .../netReproduction[alate]
              .phaseInflow = .../susceptible/adult/apterous/fecundity[phaseOutflow]
              .phaseTimeStep = .../fungusTime[step]
              .phaseK = random/k[value]
              .phaseDuration = random/lethalTimeAdult[value]
            }
          }
          StageAndPhase alate {
            .inflow = .../immigration[exposed]
            .timeStep = .../time[step]
            .duration = param[adultDuration]
            .k = random/k[value]
            .phaseInflow = .../susceptible/adult/alate[phaseOutflow]
            .phaseTimeStep = .../fungusTime[step]
            .phaseK = random/k[value]
            .phaseDuration = random/lethalTimeAdult[value]
            StageAndPhase fecundity {
              .timeStep = .../time[step]
              .duration = param[fecundityDuration]
              .k = param[fecundity_k]
              .growthFactor = .../netReproduction[alateExposed] // .../netReproduction[apterous]
              .phaseInflow = .../susceptible/adult/alate/fecundity[phaseOutflow]
              .phaseTimeStep = .../fungusTime[step]
              .phaseK = random/k[value]
              .phaseDuration = random/lethalTimeAdult[value]
            }
          }
        } // adult
        CadaverConversion succumbed {
          .succumbedApterousNymphs = .../nymph/apterous[phaseOutflow]
          .succumbedAlateNymphs    = .../nymph/alate[phaseOutflow]  
          .succumbedApterousAdult  = .../adult/apterous[phaseOutflow]
          .succumbedAlateAdults    = .../adult/alate[phaseOutflow]   
        }
      } // exposed
      Box infectious {
        OnOff isSporulating {
          .x = weather[RHmax]
          .xOn = random/sporulationOn[value]
          .xOff = 100 // ? why not 0 when it is off?
        }
        CadaverTime time {
          .isSporulating = ../isSporulating[isOn]
          .timeStep = .../fungusTime[step]
          .rhAccelaration = random/timeAcceleration[value] //2.33
        }
        Stage cadavers {
          .inflow = .../exposed/succumbed[cadavers]
          .timeStep = .../time[step]
          .duration = random/cadaverDuration[value]
          .k = random/k[value]
        }
        InfectionRate infectionRate {
          .isSporulating = ../isSporulating[isOn]
          .cadavers = ../cadavers[content]
          .transmissionEfficiency = random/transmissionEfficiency[value] 
        }
      } // infectious
      Box diagnostics {
        Accumulator aphidDays {
          .change = ../../density[total]
        }
        Accumulator cadaverDays {
          .change = ../../infectious/cadavers[content]
        }
        ThresholdNorway thresholdNorway {
          .aphids = ../../density[total]
          .cropGrowthStage = cropGrowthStage[value]
        }
        ThresholdSweden thresholdSweden {
          .aphids = ../../density[total]
          .cropGrowthStage = cropGrowthStage[value]
       }
        AphidIndex aphidIndex {
          .nymphs = ../../density[nymphs]
          .adults = ../../density[adults]
        }
        aphid::Yield yield {
          .aphidIndex = ../aphidIndex[value]
          .cropGrowthStage = cropGrowthStage[value]
        }
      } // diagnostics
    } // aphid
  } // maker
  
  Biocontrol biocontrol {
    .aphidDaysUncontrolled = withoutFungus/diagnostics/aphidDays[value] 
    .aphidDaysControlled = withFungus/diagnostics/aphidDays[value] 
    .yieldUncontrolled = withoutFungus/diagnostics/yield[yield] 
    .yieldControlled = withFungus/diagnostics/yield[yield] 
  }
  
  //
  // Output
  //
  
  OutputR {
    PageR {
      .xAxis = (
        random/fileNumber[value]
        random/cropAtStart[value]
        random/sporulationOn[value]
        random/cadaverDuration[value]
        random/propExposedImmigrants[value]
        random/transmissionEfficiency[value]
        random/lethalTimeNymph[value]
        random/timeAcceleration[value] )
      PlotR {
        .ports = biocontrol[*]|end
        .ggplot = "geom_point(size=2, alpha=0.3) + geom_smooth(colour='yellow',size=1) + theme_classic()"
      }
    }
  }
}