Simulation sim {
  .steps=273 // end of september

  Calendar calendar {
    .timeStep=1
    .timeUnit="d" 
    .initialDateTime = 1/1/2017
  }
  
  Records weather {
    .fileName= "weather/Rygge_2017.txt"
  } 
  
  //
  // Wheat
  //
  
  Box wheat{
    CropGrowthStage cropGrowthStage {
      .valueAtStart=  20
      .dayDegrees = ./time[total]
      .dayDegreesHalfways = 720
      CropIsGrowing isGrowing {
        .temperature = weather[Tavg] 
      }
      DayDegrees time{
        .T = weather[Tavg]
        .T0 = 0
        .isTicking = ../isGrowing[value]
      }
    }
  }

  //
  // Aphid
  //
  
  Box aphid {
    Box common {
      +juvenileApterousDuration = 178.5
      +juvenileAlateDuration = 203.6
      +adultDuration = 300
      +k = 25
    }
    DayDegrees time{
      .T = weather[Tavg]
      .T0 = 3 
      .Topt = 18 
      .Tmax = 30 
    }  
    Box density {
      +nymphs|sum = ../*/nymph/*[content] 
      +adults|sum = ../*/adult/*[content] 
      +total|sum  = ../*/*/*[content]     
    }
    AphidImmigration immigration {
      .cropGrowthStage = cropGrowthStage[value]
      .immigrationRate = 0.015
      .propExposedImmigrants = 0.5
      .k = .../common[k]
    }
    AphidNetReproduction netReproduction {
      .Tmin = 3 
      .Topt = 16.1 
      .Tmax = 30
      .R0opt = 51.6
      .temperature = weather[Tavg]
      .cropGrowthStage = cropGrowthStage[value]
      .aphidDensity = ../density[total]
      .aphidDensity = ../density[total]
      .aphidDensityThreshold = 40
    }
    AphidJuvenileSurvival survival{
      .cropGrowthStage = cropGrowthStage[value]
      .temperature = weather[Tavg]
    }
    Box susceptible {
      Box nymph {
        Stage apterous {
          .timeStep = .../time[step]
          .k = .../common[k]
          .duration = .../common[juvenileApterousDuration]
          .growthFactor = .../survival[value]
        }
        Stage alate {
          .timeStep = .../time[step]
          .k = .../common[k]
          .duration = .../common[juvenileAlateDuration]
          .growthFactor = .../survival[value]
        }
      }
      Box adult {
        Stage apterous {
          .inflow = .../nymph/apterous[outflow]
          .timeStep = .../time[step]
          .duration = .../common[adultDuration]
          .k = .../common[k]
        }
        Stage alate {
          .inflow = .../immigration[susceptible]
          .timeStep = .../time[step]
          .duration = .../common[adultDuration]
          .k = .../common[k]
        }
      }
    } // susceptible
  } // aphid
  
  OutputR {
    +ggplot = "geom_line(size=1.1) + scale_x_datetime(breaks = date_breaks('months'), labels = date_format('%b'))"
    PageR {
      .xAxis = calendar[date] 
      .title = "Aphids"
      .ncol = 2
      PlotR {
        +cropGrowthStage = cropGrowthStage[value]
        +apterousNetReproduction = netReproduction[apterous]
        +alateNetReproduction = netReproduction[alate]
        .ports = (.[cropGrowthStage] .[apterousNetReproduction] .[alateNetReproduction])
        .ggplot = ../..[ggplot] 
      }
      PlotR {
        +immigration = immigration[total]
        .ports = (.[immigration] susceptible/*/*[content] density[total])
        .ggplot = ../..[ggplot] 
      }
    }
  }
}