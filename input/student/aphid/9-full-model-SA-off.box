Simulation sim {
  .steps =  273 // end of september
  Box random {
    RandomiserMonteCarlo randomiser {
      .useFixed = TRUE
      .doSensitivityAnalysis = FALSE
    }
    RandomNormal cropAtStart {
      .min = 10
      .fixed = 20
      .max = 27
    }
    RandomNormal cropHalfways {
      .min = 747
      .fixed = 800
      .max = 843
    }
    RandomNormal sporulationOn {
      .min = 90
      .fixed = 92.5
      .max = 95
    }
    RandomNormal cadaverDuration {
      .min = 54
      .fixed = 112
      .max = 130
    }
    RandomNormal propExposedImmigrants {
      .min = 0.1
      .fixed = 0.5
      .max = 0.7
    }
    RandomNormal transmissionEfficiency {
      .min = 0.05
      .fixed = 0.1728
      .max = 0.1728
    }
    RandomNormal lethalTimeNymph {
      .min = 60
      .fixed = 73
      .max = 180
    }
    RandomNormal lethalTimeAdult {
      .min = 60
      .fixed = 72
      .max = 180
    }
  }
  Box param {
    +juvenileApterousDuration = 178.5
    +juvenileAlateDuration = 203.6
    +adultDuration = 300
    +k = 15
    +fecundityDuration = 100
    +fecundity_k = 1
  }

  Calendar calendar {
    .timeStep=1
    .timeUnit="d" 
    .initialDateTime = 1/1/2017
  }
  
  Records weather {
    .fileName= "weather/Rygge_2017.txt"
  } 
  
  //
  // Wheat
  //
  
  Box wheat{
    CropGrowthStage cropGrowthStage {
      .valueAtStart=  random/cropAtStart[value]
      .dayDegrees = ./time[total]
      .dayDegreesHalfways = 720
      CropIsGrowing isGrowing {
        .temperature = weather[Tavg] 
      }
      DayDegrees time{
        .T = weather[Tavg]
        .T0 = 0
        .isTicking = ../isGrowing[value]
      }
    }
  }
  
  //
  // Aphid
  //
  
  Maker maker {
    .names = ("withoutFungus" "withFungus")
    Box aphid {
      DayDegrees time{
        .T = weather[Tavg]
        .T0 = 3 
        .Topt = 18 
        .Tmax = 30 
      }  
      DayDegrees fungusTime {
        .T = weather[Tavg]
        .T0 = 2 
        .Topt = 18 
        .Tmax = 30 
      }
      Box density {
        +nymphs|sum = ../*/nymph/*[content] 
        +adults|sum = ../*/adult/*[content] 
        +total|sum  = ../*/*/*[content]     
      }
      AphidImmigration immigration {
        .cropGrowthStage = cropGrowthStage[value]
        .immigrationRate = 0.015
        .propExposedImmigrants = random/propExposedImmigrants[value]
        .k = param[k]
      }
      AphidNetReproduction netReproduction {
        .Tmin = 3 
        .Topt = 16.1 
        .Tmax = 30
        .R0opt = 51.6
        .temperature = weather[Tavg]
        .cropGrowthStage = cropGrowthStage[value]
        .aphidDensity = ../density[total]
        .aphidDensityThreshold = 40
        .exposureCost = 0.3
      }
      AphidOffspring offspring {
        .offspringFromSusceptible|sum = ../susceptible/adult/*/fecundity[outflow] 
        .offspringFromExposedApterous|sum = ../exposed/adult/apterous/fecundity[outflow] 
        .offspringFromExposedAlate|sum = ../exposed/adult/alate/fecundity[outflow] 
        .aphidDensity = ../density[total]
        .cropGrowthStage = cropGrowthStage[value]
      }
      AphidJuvenileSurvival survival{
        .cropGrowthStage = cropGrowthStage[value]
        .temperature = weather[Tavg]
      }
      Box susceptible {
        Box nymph {
          Stage apterous {
            .inflow = .../offspring[apterous]
            .timeStep = .../time[step]
            .k = param[k]
            .duration = param[juvenileApterousDuration]
            .growthFactor = .../survival[value]
            .phaseOutflowProportion = .../infectious/infectionRate[value]
          }
          Stage alate {
            .inflow = .../offspring[alate]
            .timeStep = .../time[step]
            .k = param[k]
            .duration = param[juvenileAlateDuration]
            .growthFactor = .../survival[value]
            .phaseOutflowProportion = .../infectious/infectionRate[value]
          }
        }
        Box adult {
          Stage apterous {
            .inflow = .../nymph/apterous[outflow]
            .timeStep = .../time[step]
            .duration = param[adultDuration]
            .k = param[k]
            .phaseOutflowProportion = .../infectious/infectionRate[value]
            Stage fecundity {
              .inflow = .../nymph/apterous[outflow]
              .timeStep = .../time[step]
              .duration = param[fecundityDuration]
              .k = param[fecundity_k]
              .growthFactor = .../netReproduction[apterous]
              .phaseOutflowProportion = .../infectious/infectionRate[value]
            }
          }
          Stage alate {
            .inflow = .../immigration[susceptible]
            .timeStep = .../time[step]
            .duration = param[adultDuration]
            .k = param[k]
            .phaseOutflowProportion = .../infectious/infectionRate[value]
            Stage fecundity {
              .inflow = .../immigration[susceptible]
              .timeStep = .../time[step]
              .duration = param[fecundityDuration]
              .k = param[fecundity_k]
              .growthFactor = .../netReproduction[alate]
              .phaseOutflowProportion = .../infectious/infectionRate[value]
            }
          }
        }
      } // susceptible
      Box exposed {
        +lethalTimeNymph = 73
        +lethalTimeAdult = 72
        Box nymph {
          StageAndPhase apterous {
            .timeStep = .../time[step]
            .k = param[k]
            .duration = param[juvenileApterousDuration]
            .growthFactor = .../survival[value]
            .phaseInflow = .../susceptible/nymph/apterous[phaseOutflow]
            .phaseTimeStep = .../fungusTime[step]
            .phaseK = param[k]
            .phaseDuration = random/lethalTimeNymph[value]
          }
          StageAndPhase alate {
            .timeStep = .../time[step]
            .k = param[k]
            .duration = param[juvenileAlateDuration]
            .growthFactor = .../survival[value]
            .phaseInflow = .../susceptible/nymph/alate[phaseOutflow]
            .phaseTimeStep = .../fungusTime[step]
            .phaseK = param[k]
            .phaseDuration = random/lethalTimeNymph[value]
          }
        } // nymph
        Box adult {
          StageAndPhase apterous {
            .inflow = .../nymph/apterous[outflow]
            .timeStep = .../time[step]
            .duration = param[adultDuration]
            .k = param[k]
            .phaseInflow = .../susceptible/adult/apterous[phaseOutflow]
            .phaseTimeStep = .../fungusTime[step]
            .phaseK = param[k]
            .phaseDuration = random/lethalTimeAdult[value]
            StageAndPhase fecundity {
              .timeStep = .../time[step]
              .duration = param[fecundityDuration]
              .k = param[fecundity_k]
              .growthFactor = .../netReproduction[alate]  // reduce this
              .phaseInflow = .../susceptible/adult/apterous/fecundity[phaseOutflow]
              .phaseTimeStep = ..[phaseTimeStep]
              .phaseK = param[k]
              .phaseDuration = ..[phaseDuration]
            }
          }
          StageAndPhase alate {
            .inflow = .../immigration[exposed]
            .timeStep = .../time[step]
            .duration = param[adultDuration]
            .k = param[k]
            .phaseInflow = .../susceptible/adult/alate[phaseOutflow]
            .phaseTimeStep = .../fungusTime[step]
            .phaseK = param[k]
            .phaseDuration = random/lethalTimeAdult[value]
            StageAndPhase fecundity {
              .timeStep = .../time[step]
              .duration = param[fecundityDuration]
              .k = param[fecundity_k]
              .growthFactor = .../netReproduction[apterous] // reduce this
              .phaseInflow = .../susceptible/adult/alate/fecundity[phaseOutflow]
              .phaseTimeStep = ..[phaseTimeStep]
              .phaseK = param[k]
              .phaseDuration = ..[phaseDuration]
            }
          }
        } // adult
        CadaverConversion succumbed {
          .succumbedApterousNymphs = .../nymph/apterous[phaseOutflow]
          .succumbedAlateNymphs    = .../nymph/alate[phaseOutflow]  
          .succumbedApterousAdult  = .../adult/apterous[phaseOutflow]
          .succumbedAlateAdults    = .../adult/alate[phaseOutflow]   
        }
      } // exposed
      Box infectious {
        OnOff isSporulating {
          .x = weather[RHmax]
          .xOn = random/sporulationOn[value]
          .xOff = 100
        }
        CadaverTime time {
          .isSporulating = ../isSporulating[isOn]
          .timeStep = .../fungusTime[step]
          .rhAccelaration = 2.33
        }
        Stage cadavers {
          .inflow = .../exposed/succumbed[cadavers]
          .timeStep = .../time[step]
          .duration = random/cadaverDuration[value]
          .k = param[k]
        }
        InfectionRate infectionRate {
          .isSporulating = ../isSporulating[isOn]
          .cadavers = ../cadavers[content]
          .transmissionEfficiency = random/transmissionEfficiency[value] 
        }
      } // infectious
      Box diagnostics {
        Accumulator aphidDays {
          .change = ../../density[total]
        }
        Accumulator cadaverDays {
          .change = ../../infectious/cadavers[content]
        }
        ThresholdNorway thresholdNorway {
          .aphids = ../../density[total]
          .cropGrowthStage = cropGrowthStage[value]
        }
        ThresholdSweden thresholdSweden {
          .aphids = ../../density[total]
          .cropGrowthStage = cropGrowthStage[value]
       }
        AphidIndex aphidIndex {
          .nymphs = ../../density[nymphs]
          .adults = ../../density[adults]
        }
        aphid::Yield yield {
          .aphidIndex = ../aphidIndex[value]
          .cropGrowthStage = cropGrowthStage[value]
        }
      } // diagnostics
    } // aphid
  } // maker
  
  Biocontrol biocontrol {
    .aphidDaysUncontrolled = withoutFungus/diagnostics/aphidDays[value] 
    .aphidDaysControlled = withFungus/diagnostics/aphidDays[value] 
    .yieldUncontrolled = withoutFungus/diagnostics/yield[yield] 
    .yieldControlled = withFungus/diagnostics/yield[yield] 
  }
  
  //
  // Output
  //
  
  OutputR {
    +ggplot = "geom_line(size=1.1) + scale_x_datetime(breaks = date_breaks('months'), labels = date_format('%b'))"
    PageR {
      .xAxis = calendar[date] 
      .ncol = 4
      PlotR {
        +withFungusImmigrationExposed|sum = withFungus/immigration[exposed]
        +withoutFungusImmigrationExposed|sum = withoutFungus/immigration[exposed]
        .layout = "merged"
        .ports = (.[withFungusImmigrationExposed] .[withoutFungusImmigrationExposed] cadavers[content])
        .ggplot = ../..[ggplot] 
      }
      PlotR {
        .ports = (withoutFungus/susceptible/*/*[content])
        .ggplot = ../..[ggplot] 
      }
      PlotR {
        .ports = (withFungus/susceptible/*/*[content])
        .ggplot = ../..[ggplot] 
      }
      PlotR {
        .ports = (withFungus/exposed/*/*[content])
        .ggplot = ../..[ggplot] 
      }
    }
  }

}