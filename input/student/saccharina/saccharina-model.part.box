// Part of a box script. To be included into another box script.
  Calendar calendar {
    .initialDateTime = setup[initialDateTime]
    .timeStep = 1
    .timeUnit = "h"
  }
  Records env {
    .fileName = setup[fileNamePath]
    .overrideCalendarYear = TRUE
  }
  Structure structure {
    .initMass = parameters[Winit]
    .allocation = allocation[structuralMassGrowth]
  }     
  Area area {
    .plantDensity = parameters[plantDensity]
    .frondAngle = parameters[frondAngle]
  }
  Box reserves {
    Reserves carbon {
      .initialProportion = parameters[Cinit]
      .maxProportion = parameters[Cmax]
      .allocatedProportion = allocation[reservesProportionC]
    }
    Reserves nitrogen {
      .initialProportion = parameters[Ninit]
      .maxProportion = parameters[Nmax]
      .allocatedProportion = allocation[reservesProportionN]
    }
  }
  Box demand {
    Box carbon {
      DemandCarbonStructure structure {
        .maxGrowthRate = parameters[g] 
        .fTemp     = ./fTemp[value]
        .fSalinity = ./fSalinity[value]
        .fArea     = ./fArea[value]
        Trapezoid fTemp {
          .T = env[T]
          .T0    = parameters[T0]
          .Topt1 = parameters[Topt1]
          .Topt2 = parameters[Topt2]
          .Tmax  = parameters[Tmax]
        }
        Fsalinity fSalinity {
        }
        Farea fArea {
          .aMax = parameters[aMax]
          .a50  = parameters[a50]
          .aSlope = parameters[aSlope]
        }
      }
      DemandCarbonRespiration respiration {
        .resp20 = parameters[resp20]
        .Q10 = parameters[Q10]
      }
      DemandCarbonReserves reserves {
      }
      DemandCarbonExudation exudation {
        .gamma = parameters[gamma]
      }
      DemandCarbonErosion erosion {
        .eMax = parameters[eMax]
        .e50 = parameters[e50]
      }
    }
    Sum carbonTotal {
      .values = ../carbon/*[value]
    }
    Box nitrogen {
      DemandNitrogenStructure structure {
      }
      DemandNitrogenReserves reserves {
      }
    }
    Sum nitrogenTotal {
      .values = ../nitrogen/*[value]
    }
  }
  Box supply {
    Photosynthesis photosynthesis {
      .alpha = parameters[alpha]
      .fTemp  = ./fTemp[value]
      .fNitrogen = ./fNitrogen[value]
      Trapezoid fTemp {
        .T = env[T]
        .T0    = parameters[T0]
        .Topt1 = parameters[Topt1]
        .Topt2 = parameters[Topt2]
        .Tmax  = parameters[Tmax]
      }
      Fnitrogen fNitrogen {
        .minValue = parameters[fNitrogenMin]
        .shape = parameters[shape]
      }
    }
    NitrogenUptake nitrogenUptake {
      .beta = parameters[beta]
      Fcurrent fCurrent {
      .uMin = parameters[uMin]
      }
    }
  }
  Allocation allocation {
  }
  saccharina::Biomass biomass {
    .plantDensity = area[plantDensity]
    .Cstruct = 0.21
    .Nstruct = 0.02
  }
  Calibration calibration {
    .obsFileName = setup[obsFile]
  }
  Box total {
    Accumulator growth {
      .change = allocation[supplyCarbonStructure]
    }
    Accumulator exudation {
      .change = allocation[supplyCarbonExudation]
    }
    Accumulator erosion {
      .change = demand/carbon/erosion[value]
    }
    Accumulator respiration {
      .change = demand/carbon/respiration[value]
    }
    Accumulator structuralLoss {
      .change = allocation[structuralMassLoss]
    }
  }
