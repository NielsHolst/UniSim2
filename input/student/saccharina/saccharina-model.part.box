// Part of a box script. To be included into another box script.
  Calendar calendar {
    .initialDateTime = setup[initialDateTime]
    .timeStep = 1
    .timeUnit = "h"
  }
  Records env {
    .fileName = setup[fileNamePath]
    .overrideCalendarYear = TRUE
  }
  Structure structure {
    .initMass = parameters[Winit]
    .allocation = allocation[structuralMassGrowth]
  }     
  Area area {
  }
  Box reserves {
    Reserves carbon {
      .initialProportion = parameters[Cinit]
      .maxProportion = 0.8
      .allocatedProportion = allocation[reservesProportionC]
    }
    Reserves nitrogen {
      .initialProportion = parameters[Ninit]
      .maxProportion = 0.044 //0.022
      .allocatedProportion = allocation[reservesProportionN]
    }
  }
  Box demand {
    Box carbon {
      DemandCarbonStructure structure {
        .maxGrowthRate = parameters[g] 
        .fTemp     = ./fTemp[value]
        .fSalinity = ./fSalinity[value]
        .fArea     = ./fArea[value]
        Trapezoid fTemp {
          .T = env[T]
          .T0    = parameters[demandT0]
          .Topt1 = parameters[demandTopt1]
          .Topt2 = parameters[demandTopt2]
          .Tmax  = parameters[demandTmax]
        }
        Fsalinity fSalinity {
        }
        Farea fArea {
          .aMax = parameters[aMax]
          .a50  = parameters[a50]   
        }
      }
      DemandCarbonRespiration respiration {
      }
      DemandCarbonReserves reserves {
      }
      DemandCarbonExudation exudation {
        .gamma = parameters[gamma]
      }
      DemandCarbonErosion erosion {
      }
    }
    Sum carbonTotal {
      .values = ../carbon/*[value]
    }
    Box nitrogen {
      DemandNitrogenStructure structure {
      }
      DemandNitrogenReserves reserves {
      }
    }
    Sum nitrogenTotal {
      .values = ../nitrogen/*[value]
    }
  }
  Box supply {
    Photosynthesis photosynthesis {
      .k = parameters[k]
      .alpha = parameters[alpha]
      .fTemp  = ./fTemp[value]
      .fNitrogen = ./fNitrogen[value]
      Trapezoid fTemp {
        .T = env[T]
        .T0    = parameters[supplyT0]
        .Topt1 = parameters[supplyTopt1]
        .Topt2 = parameters[supplyTopt2]
        .Tmax  = parameters[supplyTmax]
      }
      Fnitrogen fNitrogen {
        .minValue = parameters[fNitrogenMin]
      }
    }
    NitrogenUptake nitrogenUptake {
      .beta = parameters[beta]
      Fcurrent fCurrent {
      .uMin = parameters[uMin]
      }
    }
  }
  Allocation allocation {
  }
  Biomass biomass {
  }
  Box total {
    Accumulator growth {
      .change = allocation[supplyCarbonStructure]
    }
    Accumulator exudation {
      .change = allocation[supplyCarbonExudation]
    }
    Accumulator erosion {
      .change = demand/carbon/erosion[value]
    }
    Accumulator respiration {
      .change = demand/carbon/respiration[value]
    }
    Accumulator structuralLoss {
      .change = allocation[structuralMassLoss]
    }
  }
  OutputR output {
    .end = setup[endR]
    Box value {
      +area = area[value]
    }
    OutputText {
      .ports = (calendar[dateTime] output/value[area] biomass[dryWeight]
                 fArea[value] fNitrogen[value] structure/fTemp[value] fSalinity[value] fCurrent[value] photosynthesis[sdRatio]
                 total/*[value]
                 biomass[dryWeight] biomass[carbonPct] biomass[nitrogenPct]
                 setup[envFile] setup[obsFile] setup[trial])
    }
  }
