Simulation sim {
  .steps =  365 
  Calendar calendar {
    .initialDateTime = 1/1/2001
  }
  Hump Twater {
    .x = calendar[dayOfYear]
    .x0 = 45
    .x1 = 365
    .ymin = 2.5
    .ymax = 19
  }
  Briere time {
    .a = 7.09e-5
    .T0 = 11.1
    .Tmax = 28.8
    .T = Twater[value]
  }
  Box Chironimus {
    Stage adult {
      .inflow = ../pupa[outflow]
      .timeStep = 1
      .duration = 40
    }
    Box fecundity {
      +eggsLaid = ./reproduction[outflow]
      Stage reproduction {
        .inflow = ../../adult[inflow]
        .timeStep = ../../adult[timeStep]
        .duration = 20
        .growthFactor = 30 // life time fecundity per female times sex ratio 
      }
    }
    Stage egg {
      .inflow = ../fecundity[eggsLaid]
      .timeStep = time[step]
      .duration = 0.167  // = 3.76/22.5
    }
    Stage larva {
      .inflow = ../egg[outflow]
      .initial = 100
      .timeStep = time[step]
      .duration = 1
    }
    Stage pupa {
      .inflow = ../larva[outflow]
      .timeStep = time[step]
      .duration = 0.044  // = 1/22.5
    }
  }
  OutputR {
    PageR {
      .xAxis = calendar[date]
      +ggplot1 = "geom_line() +
                 labs(title='Tentative water temperature', x='Month', y='Twater') +
                 scale_x_datetime(breaks = date_breaks('1 month'), labels = date_format('%m'))"
      +ggplot2 = "geom_line() +
                 labs(title='Chironimus', x='Month', y='Density') +
                 scale_x_datetime(breaks = date_breaks('1 month'), labels = date_format('%m')) +
                 scale_y_continuous(trans='log10', limits=c(1,NA), breaks=10^(0:5))"
      PlotR {
        .layout = "merged"
        .ports = (Twater[value])
        .ggplot = ..[ggplot1]
      }
      PlotR {
        .layout = "merged"
        .ports = (adult[content] egg[content] larva[content] pupa[content])
        .ggplot = ..[ggplot2]
      }
    }
  }
}