// P.Coffea4.box
Simulation sim {
  .steps = 365
  Calendar calendar {
    .initialDateTime = 1/1/2018 
  }
  Records weather {
    .fileName = "weather/CAMINAS.txt"
  }
  
  Box Parasitoid {
    OnOff immigration {  
      .x = calendar[dayOfYear]
      .xOn = 244  // 1st of  June
      .xOff =  303 //  30th of September //Vieira, 2008
      .valueOn = 0.1
      .valueOff = 0
      DayDegrees dayDegrees {
        .T0 = 16.02 // 13,7 Gutierrez et al., 1998. 16,02 Rodriguez et al., 2017
        .Topt = 25
        .Tmax = 33.64 // Rodriguez et al., 2017. 32 Gutierrez et al., 1998
        .T = weather[Tavg]
      }
    }
    Stage adult {
      .inflow|sum = ../../pupa[outflow] | immigration[value]
      .duration = 30
      .timeStep = 1
      .growthFactor = 17.1 // R0 = 18 eggs. Sex ratio 20:1. (Ro = 18*0,05 = 0,9. 0,9-18= 17,1) // Feldhege 1992. 0,6 Rodriguez et al., 2017
    }
    Stage oviposition {
      .inflow = ../adult[inflow]   
      .duration = ../adult[duration]   
      .timeStep = ../adult[timeStep]   
      .growthFactor = 17.1 // R0 = 18 eggs. Sex ratio 20:1. (Ro = 18*0,05 = 0,9. 0,9-18= 17,1) // Feldhege 1992. 0,6 Rodriguez et al., 2017
    }
    Stage egg {
      .inflow = ../oviposition[outflow]   
      .duration = 36
      .timeStep = ./time[step]  
      DayDegrees time {
        .T0 = 16.02 // 13,7 Gutierrez et al., 1998. 16,02 Rodriguez et al., 2017
        .Topt = 25
        .Tmax = 33.64 // Rodriguez et al., 2017. 32 Gutierrez et al., 1998
        .T = weather[Tavg]
      }
    }
    Stage larva {
      .inflow = ../egg[outflow]
      .duration = 276
      .timeStep = ./time[step]
      DayDegrees time {
        .T0 = 16.02 // 13,7 Gutierrez et al., 1998. 16,02 Rodriguez et al., 2017
        .Topt = 25
        .Tmax = 33.64 // Rodriguez et al., 2017. 32 Gutierrez et al., 1998
        .T = weather[Tavg]
      }
    }
    Stage pupa {
      .inflow = ../larva[outflow]
      .duration = 324
      .timeStep = ./time[step]
      DayDegrees time {
        .T0 = 16.02 // 13,7 Gutierrez et al., 1998. 16,02 Rodriguez et al., 2017
        .Topt = 25
        .Tmax = 33.64 // Rodriguez et al., 2017. 32 Gutierrez et al., 1998
        .T = weather[Tavg]
      }
    }
  }
  OutputR {
    PageR {
      .xAxis = calendar[date]
      .ncol = 3  
      +monthBreaks = "scale_x_datetime(
                        breaks = date_breaks('2 months'),
                        labels = date_format('%b')
                      )"  
      PlotR {
        .ports = *[content]
        .ggplot = ..[monthBreaks]
      }
      PlotR {
        .ports = *[outflowTotal]
        .ggplot = ..[monthBreaks]
      }
      PlotR {  
        +immigration = immigration[value]
        .ports = (calendar[dayOfYear]  .[immigration] weather[Tavg])
        .ggplot = ..[monthBreaks]
      }
    }
  }
}

