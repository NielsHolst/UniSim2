Simulation maruca {
  .steps = 365
  Box parameters {
    +pestPressure = 0.32 //(Ongunwolu, 1990)
  }
  Scenarios scenarios {
    .fileName = "weather400/weather400.txt" // (NOAA,2018)
    .title = "FileName"
  }
  Calendar calendar {
    .initialDateTime = 1/1/2017
  }
  Records weather400 {
    .fileName = scenarios[FileName]
  }
  Box cowpea { 
    Stage initial {
      .initial = parameters[pestPressure]
      .duration = 307 //(Cavalcante et al., 2016)
      .timeStep= ./time[value]
      OnOff time  {
        .x= calendar [dayOfYear]
        .xOn = 166 // 15th of june (Abdou Razakou Ibrahim et al., 2018)
        .xOff = 196 //15th of july 
        .valueOn=./dayDegrees[step]
        .valueOff= 0
        DayDegrees dayDegrees {
          .T0 = 8 // (Pasquet and Baudoin, 1997)
          .Topt = 28 // (Carvalho et al., 2017)
          .T = weather400[Tavg]
        }
      }
    }
    Stage vegetative {
      .inflow = ../initial[outflow]
      .duration = 326 //(Cavalcante et al., 2016)
      .timeStep= ./time[step]
      DayDegrees time {
        .T0 = 8 // (Pasquet and Baudoin, 1997)
        .Topt = 28 // (Carvalho et al., 2017)
        .T = weather400[Tavg]
      }
    }
    Stage flowering {
      .inflow = ../vegetative[outflow]
      .duration = 463 //(Cavalcante et al., 2016)
      .timeStep= ./time[step]
      DayDegrees time {
        .T0 = 8 // (Pasquet and Baudoin, 1997)
        .Topt = 28 // (Carvalho et al., 2017)
        .T = weather400[Tavg]
      }
    }
    Stage maturation {
      .inflow = ../flowering[outflow]
      .duration = 265 //(Cavalcante et al., 2016)
      .timeStep= ./time[step]
      DayDegrees time {
        .T0 = 8 // (Pasquet and Baudoin, 1997)
        .Topt = 28 // (Carvalho et al., 2017)
        .T = weather400[Tavg]
      }
    }
  }
  Box maruca {
    Stage egg {
      .inflow = ../adult/oviposition[outflow]
      .duration = 51.1 // (Adati et al., 2004)
      .timeStep = ./time[step]
	  .growthFactor = 0.925
      DayDegrees time {
        .T0 = 10.5 // (Adati et al., 2004)
        .Topt = 28 // (Dannon et al., 2010)  
        .Tmax = 31.9 // (Adati et al., 2004)
        .T = weather400[Tavg]
      }
    }
    Stage instar1 {
      .inflow = ../egg[outflow]
      .duration = 50.8 // (Adati et al., 2004)
      .timeStep= ./time[step]
	  .growthFactor = 0.93719
      DayDegrees time {
        .T0 = 11.5 // (Adati et al., 2004)
        .Topt = 28 // (Dannon et al., 2010) 
        .Tmax = 31.9 // (Adati et al., 2004)
        .T = weather400[Tavg]
      }
    }
    Stage instar2 {
      .inflow = ../instar1[outflow]
      .duration = 40.0 // (Adati et al., 2004)
      .timeStep= ./time[step]
	  .growthFactor = 0.93719
      .instantLossRate = web/marucaInstar2[lossRatio] // (Dung et al., 2011)
      DayDegrees time {
        .T0 = 10.4 // (Adati et al., 2004)
        .Topt = 28 // (Dannon et al., 2010) 
        .Tmax = 31.9 // (Adati et al., 2004)
        .T = weather400[Tavg]
      }
    }
    Stage instar3 {
      .inflow = ../instar2[outflow]
      .duration = 40.1 // (Adati et al., 2004)
      .timeStep= ./time[step]
	  .growthFactor = 0.93719
      .instantLossRate = web/marucaInstar3[lossRatio] // (Dung et al., 2011)
      DayDegrees time {
        .T0 = 10.4 // (Adati et al., 2004)
        .Topt = 28 // (Dannon et al., 2010)  
        .Tmax = 31.9 // (Adati et al., 2004)
        .T = weather400[Tavg]
      }
    }
    Stage instar4 {
      .inflow = ../instar3[outflow]
      .duration = 51.8 // (Adati et al., 2004)
      .timeStep= ./time[step]
	  .growthFactor = 0.93719
      DayDegrees time {
        .T0 = 8 // (Adati et al., 2004)
        .Topt = 28 // (Dannon et al., 2010) 
        .Tmax = 31.9 // (Adati et al., 2004)
        .T = weather400[Tavg]
      }
    }
    Stage instar5 {
      .inflow = ../instar4[outflow]
      .duration = 73.6 // (Adati et al., 2004)
      .timeStep= ./time[step]
	  .growthFactor = 0.93719
      DayDegrees time {
        .T0 = 9.7 // (Adati et al., 2004)
        .Topt = 28 // (Dannon et al., 2010) 
        .Tmax = 31.9 // (Adati et al., 2004)
        .T = weather400[Tavg]
      }
    }
    Stage prepupae {
      .inflow = ../instar5[outflow]
      .duration = 28.2 // (Adati et al., 2004)
      .timeStep= ./time[step]
	  .growthFactor = 0.935
      DayDegrees time {
        .T0 = 7.7 // (Adati et al., 2004)
        .Topt = 28 // (Dannon et al., 2010) 
        .Tmax = 31.9 // (Adati et al., 2004)
        .T = weather400[Tavg]
      }
    }
    Stage pupae {
      .inflow = ../prepupae[outflow]
      .duration = 116.5 // (Adati et al., 2004)
      .timeStep= ./time[step]
	  .growthFactor = 0.935
      DayDegrees time {
        .T0 = 10.9 // (Adati et al., 2004)
        .Topt = 28 // (Dannon et al., 2010)  
        .Tmax = 31.9 // (Adati et al., 2004)
        .T = weather400[Tavg]
      }
    }
    Stage preOviposition {
      .inflow = ../pupae[outflow]
      .duration = 2 //(Aboubakar Souna, 2018)
      .timeStep = 1
      .growthFactor = ./staying[value]
      ProportionalSignal staying {
        .input = cowpea/flowering[content]
        .minSignal = 0
        .maxSignal = 1
        .threshold = 0
        .thresholdBand = 1
      }
    }
    Box adult {
      ProportionalSignal immigration {
        .input = cowpea/flowering[content]
        .minSignal = 0
        .maxSignal = parameters[pestPressure]
        .threshold = 0
        .thresholdBand = 1
      }
      Stage individuals {
        .inflow|sum = ../../preOviposition[outflow] | ../immigration[value]
        .duration = 10.24 // (Dannon et al., 2010)
        .timeStep = 1
      }
      Stage oviposition {
        .inflow = ../individuals[inflow]
        .duration = ../individuals[duration]
        .timeStep = ../individuals[timeStep]
        .growthFactor = 200 //(Onstad et al., 2012)
      }
    }
  }
  Box therophilus { // parasite
		Stage eggTJ {
			.inflow = web/therophilusAdult[supply]
			.duration = 31.04 //(Aboubakar Souna, 2018)
			.timeStep= ./time[step]
			DayDegrees time {
				.T0 = 10 // assumption
				.Topt = 26 // assumption
				.T = weather400[Tavg]
			}
		}
		Stage instar1TJ {
			.inflow = ../eggTJ[outflow]
			.duration = 48.64 //(Aboubakar Souna, 2018)
			.timeStep= ./time[step]
			DayDegrees time {
				.T0 = 10 // assumption
				.Topt = 26 // assumption
				.T = weather400[Tavg]
			} 
		}
		Stage instar2TJ {
			.inflow = ../instar1TJ[outflow]
			.duration = 32.64 //(Aboubakar Souna, 2018)
			.timeStep= ./time[step]
			DayDegrees time {
				.T0 = 10 // assumption
				.Topt = 26 // assumption
				.T = weather400[Tavg]
			} 
		}
		Stage instar3TJ {
			.inflow = ../instar2TJ[outflow]
			.duration = 29.44 //(Aboubakar Souna, 2018)
			.timeStep= ./time[step]
			DayDegrees time {
				.T0 = 10 // assumption
				.Topt = 26 // assumption
				.T = weather400[Tavg]
			} 
		}
		Stage prepupaeTJ {
			.inflow = ../instar3TJ[outflow]
			.duration = 17.76 //(Aboubakar Souna, 2018)
			.timeStep= ./time[step]
			DayDegrees time {
				.T0 = 10 // assumption
				.Topt = 26 // assumption
				.T = weather400[Tavg]
			} 
		}
		Stage pupaeTJ {
			.inflow = ../prepupaeTJ[outflow]
			.duration = 90.4 //(Aboubakar Souna, 2018)
			.timeStep= ./time[step]
			.growthFactor= ./TJstaying[value]
			DayDegrees time {
				.T0 = 10 // assumption
				.Topt = 26 // assumption
				.T = weather400[Tavg]
			} 
			ProportionalSignal TJstaying {
				.input = cowpea/flowering[content]
				.minSignal = 0
				.maxSignal = 1
				.threshold = 0
				.thresholdBand = 1
			}
		}
		Box adultTJ {
			ProportionalSignal TJimmigration {
				.input = cowpea/flowering[content]
				.minSignal = 0
				.maxSignal = 0.032 // assumption
				.threshold = 0
				.thresholdBand = 1
			}
			Stage individualsTJ {
				.inflow |sum= ../../pupaeTJ[outflow] | ../TJimmigration[value]
				.duration = 5.63 //(Aboubakar Souna, 2018)
				.timeStep = 1
			}
			Stage oviposition {
				.inflow = ../individualsTJ[inflow]
				.duration = ../individualsTJ[duration]
				.growthFactor = 41.41 //(Aboubakar Souna, 2018)
				.timeStep = ../individualsTJ[timeStep]
			}
		}
	}
	FoodWeb web {
		.attackFile = "Model_M_C_J_attack_matrix.txt" // (Dung et al., 2011)
		.gainFile = "Model_M_C_J_gain_matrix.txt"
		.showMatrices = TRUE
		FoodWebBox marucaInstar2 {
			.density = maruca/instar2[content]
		}
		FoodWebBox marucaInstar3 {
			.density = maruca/instar3[content]
		}
		FoodWebBox therophilusAdult{
			.density = therophilus/adultTJ/individualsTJ[content]
			.demand = therophilus/adultTJ/oviposition[outflow]
		}
    } 
  OutputR output {
  .end = "Model_M_C_J_400.R"
    +ggplot = "geom_line(size=1.1) + 
                    scale_x_datetime(
                      breaks = date_breaks('months'), 
                      labels = date_format('%b')
                    )
                    + 
                    ylim(-1,NA)" 
    PageR cowpea {
      .xAxis = calendar[date]
      .title = "Cowpea"
      PlotR {
        .ports = (cowpea/*[content] cowpea/maturation/*[content])
        .ggplot = output[ggplot]
      }    
    }
    PageR Maruca {
      .xAxis = calendar[date]
      .title = "Maruca population dynamics"
      PlotR {
        .ports = (maruca/*[content] maruca/adult/*[content])
        .ggplot = output[ggplot]
        .transform = "log10"
        .ncol = 2
      }
    }
	PageR Therophilus {
      .xAxis = calendar[date]
      .title = "Therophilus population dynamics"
      PlotR {
        .ports = (therophilus/*[content] therophilus/adultTJ/*[content])
        .ggplot = output[ggplot]
        .transform = "log10"
        .ncol = 2
      }
    }
    PageR {
      .xAxis = calendar[date]
      .title = "Maruca immigration and emigration"
      PlotR {
        .ports = (maruca/adult/immigration[value] maruca/preOviposition/staying[value])
        .ggplot = output[ggplot]
      }
    }
	PageR web {
			.xAxis = calendar[date]
			.ncol = 4 
			.title = "Food web"
			PlotR {
				.ports = web/*[loss]
				.ggplot = output[ggplot]
			}
			PlotR {
				.ports = web/*[lossRatio]
				.ggplot = output[ggplot]
			}
		}
	PageR temperature {
      .xAxis = calendar[date]
      .title = "Average Temperature"
	  PlotR {
        .ports = (weather400[Tavg])
        .ggplot = output[ggplot]
      }
	}
  }
}