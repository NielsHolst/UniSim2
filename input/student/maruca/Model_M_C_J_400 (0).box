// cowpea simulation GDD taken from cavalcante et al. 2016, 
//sowing time from (AbdouRzakou Ibrahim et al. 2018)
// Maruca GDD : the data are from the article Adati T, Nakamura S, Tamò M, Kawazu K. 
// Effect of temperature on development and survival of the legume pod borer, Maruca vitrata (Fabricius) (Lepidoptera : Pyralidae) reared on a semi-synthetic diet. 
// Applied Entomology and Zoology. 2004;39(1):139-45. 
Simulation LowPrecipitation {
	.steps = 365
	Box parameters {
		+pestPressure = 0.2 // number of adult arriving per day during flowering
	}
	Scenarios scenarios {
		.fileName = "weather400/weather400.txt"
	}
	Calendar calendar {
		.initialDateTime = 1/1/2017
	}
	Records weather400 {
		.fileName = scenarios[FileName] 
	}
	Box cowpea { 
		Stage initial {
			.initial = parameters[pestPressure]
			.duration = 307 
			.timeStep= ./time[value]
			OnOff time  {
				.x= calendar [dayOfYear]
				.xOn = 166 // 15th of june // Based on precipitation
				.xOff = 196 //15th of july 
				.valueOn=./dayDegrees[step]
				.valueOff= 0
				DayDegrees dayDegrees {
					.T0 = 8 
					.Topt = 28 // cavhalho, 2017
					.T = weather400[Tavg]
				}
			}
		}
		Stage vegetative {
			.inflow = ../initial[outflow]
			.duration = 326
			.timeStep= ./time[step]
			DayDegrees time {
				.T0 = 8
				.Topt = 28
				.T = weather400[Tavg]
			}
		}
		Stage flowering {
			.inflow = ../vegetative[outflow]
			.duration = 463
			.timeStep= ./time[step]
			DayDegrees time {
				.T0 = 8
				.Topt = 28
				.T = weather400[Tavg]
			}
		}
		Stage maturation {
			.inflow = ../flowering[outflow]
			.duration = 265
			.timeStep= ./time[step]
			DayDegrees time {
				.T0 = 10
				.Topt = 28
				.T = weather400[Tavg]
			}
		}
	}
	Box maruca {
		Stage egg {
			.inflow = ../adult/oviposition[outflow]
			.duration = 51.1 // adati et al. 2014
			.timeStep= ./time[step]
			DayDegrees time {
				.T0 = 10.5 // adati et al. 2014
				.Topt = 28 // Dannon et al. 2009 --> optimum btw 26-30° 
				.Tmax = 31.9 // adati et al. 2014
				.T = weather400[Tavg]
			}
		}
		Stage instar1 {
			.inflow = ../egg[outflow]
			.duration = 50.8
			.timeStep= ./time[step]
			DayDegrees time {
				.T0 = 11.5 // adati et al. 2004
				.Topt = 28 // Dannon et al. 2009 --> optimum btw 26-30° 
				.Tmax = 31.9
				.T = weather400[Tavg]
			}
		}
		Stage instar2 {
			.inflow = ../instar1[outflow]
			.duration = 40.0
			.timeStep= ./time[step]
			.instantLossRate = web/marucaInstar2[lossRatio] // Dung et al 2011
			DayDegrees time {
				.T0 = 10.4 // adati et al. 2004
				.Topt = 28 // Dannon et al. 2009 --> optimum btw 26-30° 
				.Tmax = 31.9
				.T = weather400[Tavg]
			}
		}
		Stage instar3 {
			.inflow = ../instar2[outflow]
			.duration = 40.1
			.timeStep= ./time[step]
			.instantLossRate = web/marucaInstar3[lossRatio]
			DayDegrees time {
				.T0 = 10.4 // adati et al. 2004
				.Topt = 28 // Dannon et al. 2009 --> optimum btw 26-30° 
				.Tmax = 31.9
				.T = weather400[Tavg]
			}
		}
		Stage instar4 {
			.inflow = ../instar3[outflow]
			.duration = 51.8
			.timeStep= ./time[step]
			DayDegrees time {
				.T0 = 8 // adati et al. 2004
				.Topt = 28 // Dannon et al. 2009 --> optimum btw 26-30° 
				.Tmax = 31.9
				.T = weather400[Tavg]
			}
		}
		Stage instar5 {
			.inflow = ../instar4[outflow]
			.duration = 73.6
			.timeStep= ./time[step]
			DayDegrees time {
				.T0 = 9.7 // adati et al. 2004
				.Topt = 28 // Dannon et al. 2009 --> optimum btw 26-30° 
				.Tmax = 31.9
				.T = weather400[Tavg]
			}
		}
		Stage prepupae {
			.inflow = ../instar5[outflow]
			.duration = 28.2
			.timeStep= ./time[step]
			DayDegrees time {
				.T0 = 7.7 // adati et al. 2004
				.Topt = 28 // Dannon et al. 2009 --> optimum btw 26-30° 
				.Tmax = 31.9
				.T = weather400[Tavg]
			}
		}
		Stage pupae {
			.inflow = ../prepupae[outflow]
			.duration = 116.5
			.timeStep= ./time[step]
			DayDegrees time {
				.T0 = 10.9 // adati et al. 2004
				.Topt = 28 // Dannon et al. 2009 --> optimum btw 26-30° 
				.Tmax = 31.9
				.T = weather400[Tavg]
			}
		}
		Box adult {
			+immigration = cowpea/flowering[content]
			Stage individuals {
				.inflow|sum = ../../pupae[outflow]| cowpea/flowering[content]
				.duration = 12.24 //  days mean taken from dannon et al 2009
				.timeStep = 1
			}
			Stage preOviposition {
				.inflow|sum = ../../pupae[outflow]| cowpea/flowering[content]
				.duration = 2
				.timeStep = 1
			}
			Stage oviposition {
				.inflow = ../preOviposition[outflow]
				.duration = 3 // mating only once between the 2nd and 5th night of adulthood (Souna 2018)
				.timeStep = 1
				.growthFactor = 46.69 // Mean taken from Dannon et al 2009 -- total fecundity 140.06 egg/female then divide in 3 it gives 46.69 egg/female
			}
		}
	}
	 Box Therophilus { // parasite
		Stage eggTJ {
			.initial = 30 // NEED to find data
			.inflow = ../adultTJ/oviposition[outflow]
			.duration = 31.04 //  GDD based on days data taken from (souna; 2018, these)
			.timeStep= ./time[step]
			DayDegrees time {
				.T0 = 10 // assumption
				.Topt = 26 // assumption
				.T = weather400[Tavg]
			}
		}
		Stage instar1TJ {
			.inflow = ../eggTJ[outflow]
			.duration = 48.64 //  GDD based on days data taken from (souna; 2018, these)
			.timeStep= ./time[step]
			DayDegrees time {
				.T0 = 10 // assumption
				.Topt = 26 // assumption
				.T = weather400[Tavg]
			} 
		}
		Stage instar2TJ {
			.inflow = ../instar1TJ[outflow]
			.duration = 32.64 //  GDD based on days data taken from (souna; 2018, these)
			.timeStep= ./time[step]
			DayDegrees time {
				.T0 = 10 // assumption
				.Topt = 26 // assumption
				.T = weather400[Tavg]
			} 
		}
		Stage instar3TJ {
			.inflow = ../instar2TJ[outflow]
			.duration = 29.44 //  GDD based on days data taken from (souna; 2018, these)
			.timeStep= ./time[step]
			DayDegrees time {
				.T0 = 10 // assumption
				.Topt = 26 // assumption
				.T = weather400[Tavg]
			} 
		}
		Stage prepupaeTJ {
			.inflow = ../instar3TJ[outflow]
			.duration = 17.76 //  GDD based on days data taken from (souna; 2018, these)
			.timeStep= ./time[step]
			DayDegrees time {
				.T0 = 10 // assumption
				.Topt = 26 // assumption
				.T = weather400[Tavg]
			} 
		}
		Stage pupaeTJ {
			.inflow = ../prepupaeTJ[outflow]
			.duration = 90.4 //  GDD based on days data taken from (souna; 2018, these)
			.timeStep= ./time[step]
			DayDegrees time {
				.T0 = 10 // assumption
				.Topt = 26 // assumption
				.T = weather400[Tavg]
			} 
		}
		Box adultTJ {
			Stage individualsTJ {
				.inflow = ../../pupaeTJ[outflow]
				.duration = 5.63 //  days mean taken from dannon et al 2009
				.timeStep = ./time[value]
				OnOff time  {
					.x= calendar [dayOfYear]
					.xOn = 166 // 15th of june Parasite attacks MV only on cowpea
					.xOff = 320 // mid october as it's roughly the end of cowpea season
					.valueOn=1
					.valueOff= 0
				
				}
			}
			Stage oviposition {
				.inflow = ../../pupaeTJ[outflow]
				.duration = 5.63 // mating only once between the 2nd and 5th night of adulthood (Souna 2018)
				.timeStep = 1
				.growthFactor = 16.34 // 92.03 total offsprings during the ovipostion period -- 92.03/5.63 =16.34
			}
		}
	}
	FoodWeb web {
		.attackFile = "Model_M_C_J_attack_matrix.txt" // based on Karel 1985
		.gainFile = "Model_M_C_J_gain_matrix.txt"
		.showMatrices = TRUE
		FoodWebBox marucaInstar2 {
			.density = maruca/instar2[content]
		}
		FoodWebBox marucaInstar3 {
			.density = maruca/instar3[content]
		}
		FoodWebBox therophilusAdult{
			.density = Therophilus/adultTJ/individualsTJ[content]
			.demand = 16.34 // total offspring of TJ when they parasite 3 day old MV for 1 egg they need 1 larvae so in total each adult needs 92.03 larvae for all their eggs
		}
    } 
	OutputR output {
		.end = "Model_M_C_J_400.R"
		+ggplot2 = "geom_line(size=1.1) + 
                    scale_x_datetime(
                      breaks = date_breaks('4 months'), 
                      labels = date_format('%b')
                    ) + 
                    ylim(-1,NA)" 
		+ggplot = "geom_line(size=1.1) + 
                    scale_x_datetime(
                      breaks = date_breaks('years'),
                      labels = date_format('%y')
                    ) + 
                    ylim(-1,NA)" 
		PageR Maruca {
			.xAxis = calendar[date]
			.title = "Maruca"
			PlotR {
				.ports = (maruca/*[content] maruca/adult/*[content])
				.ggplot = output[ggplot]
				.transform = "log10"
			}
		}
		PageR cowpea {
			.xAxis = calendar[date]
			.title = "cowpea"
			PlotR {
				.ports = (cowpea/*[content] cowpea/maturation/*[content])
				.ggplot = output[ggplot]
				.transform = "log10"
			}		
		}
		PageR Therophilus {
			.xAxis = calendar[date]
			.title = "therophilus"
			PlotR {
				.ports = (Therophilus/*[content] Therophilus/adultTJ/*[content])
				.ggplot = output[ggplot]
				.transform = "log10"
			}		
		}
		PageR web {
			.xAxis = calendar[date]
			.ncol = 4 
			.title = "Food web"
			PlotR {
				.ports = web/*[loss]
				.ggplot = output[ggplot]
			}
			PlotR {
				.ports = web/*[lossRatio]
				.ggplot = output[ggplot]
			}
		}
	}
	
}