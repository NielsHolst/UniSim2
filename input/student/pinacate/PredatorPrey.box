// May 11, 2017
// this script is the final iteration of my ecological model for Spring 2017 Ecological Modeling at the University of Arizona
// written by Andrew Antaya with much help from Niels Holst
Simulation sim {
  .steps = 3650
  .iterations = 1
  Calendar calendar {
    .timeStep = 1
    .timeUnit = "d"
    .initialDateTime = 1/1/2000
  }
  pinacate::Vegetation veg {
  .herbivorebiomass|sum = *<Herbivore>[value]
  .K = 100000
  .scaling = ./scaling[value]
  .currentday = calendar[dayOfYear]
  
    RandomUniform scaling {
    .minValue = 0
    .maxValue = 1
    .drawAtReset = TRUE
    }
  }
  Box sheep {
    pinacate::Herbivore juv {
      .weight = 3.9 // average ewe weight from Hass 1995
      .popsize = ./juv[content]
      
      Stage juv {
        .timeStep = 1          
        .initial = 10  
        .duration = 365
        .inflow = ../../birth[value]
        .instantLossRate = lion[juvSheepMort]
      }
    }
    pinacate::Herbivore adult {
      .weight = 90 // adult male weight from AZGFD
      .popsize = ./adult[content]
      
      Stage adult {
        .timeStep = 1          
        .initial = 10 
        .duration = 3650
        .inflow = ../../juv/juv[outflow]
        .instantLossRate = lion[adultSheepMort]
      }
    }
    pinacate::Fecundity fecundity {
      .density = ../adult/adult[content]
      .percapita = 0.5
      .availability = veg[availability]
    }
    pinacate::Birth birth {
    .birthday = 180
    .fecundity = ../fecundity[value]
    .currentday = calendar[dayOfYear]
    }
  }
  Box deer {
    pinacate::Herbivore juv {
      .weight = 1.95 // average fawn weight from Galindo-Leal and Weber 1994
      .popsize = ./juv[content]
      
      Stage juv {
        .timeStep = 1          
        .initial = 10  
        .duration = 180
        .inflow = ../../birth[value]
        .instantLossRate = lion[juvDeerMort]
      }
    }
    pinacate::Herbivore adult {
      .weight = 57 //adult male weight from AZGFD 
      .popsize = ./adult[content]
      
      Stage adult {
        .timeStep = 1          
        .initial = 10 
        .duration = 3650
        .inflow = ../../juv/juv[outflow]
        .instantLossRate = lion[adultDeerMort]
      }
    }
    pinacate::Fecundity fecundity {
      .density = ../adult/adult[content]
      .percapita = 1
      .availability = veg[availability]
    }
    pinacate::Birth birth {
    .birthday = 180
    .fecundity = ../fecundity[value]
    .currentday = calendar[dayOfYear]
    }
  }
  pinacate::Predator lion {
    .demand = 1 //fixed prey demand of 10kg/day for lion
    .juvSheepAttack = 0.25
    .juvSheepMass = sheep/juv[value]
    .adultSheepAttack = 0.10
    .adultSheepMass = sheep/adult[value]
    .juvDeerAttack = 0.50
    .juvDeerMass = deer/juv[value]
    .adultDeerAttack = 0.10
    .adultDeerMass = deer/adult[value]
    }  
  
  OutputR {
  .popUp = TRUE
    PageR {
      .xAxis = calendar[date]
      PlotR {
        .ports = (sheep/*/*[content] sheep/juv/juv[outflowTotal] sheep/fecundity[value])
      }
    }
  PageR {
     PlotR {
        .ports = (sheep/*<Herbivore>[value])
      }
    }
  PageR {
      PlotR {
        .ports = (deer/*/*[content] deer/juv/juv[outflowTotal] deer/fecundity[value])
      }
    }
  PageR {
     PlotR {
        .ports = (deer/*<Herbivore>[value])
      }
    }
  PageR {
     PlotR {
        .ports = (veg[availability])
      }
    }
  PageR {
    PlotR {
    .ports = (lion[juvSheepMort] lion[adultSheepMort] lion[juvDeerMort] lion[adultDeerMort])
      }
    }
  PageR {
    PlotR {
    .ports = (lion[juvSheepMortMass] lion[adultSheepMortMass] lion[juvDeerMortMass] lion[adultDeerMortMass])
      }
    }
  PageR {
    PlotR {
    .ports = (lion[Supply])
      }
    }
  }
}