// book/week06/butterfly02.box
Simulation sim {
	.steps = 180
	.stopIterations = scenario[atEnd]
	.useStopIterations = TRUE
	Scenarios scenario {
		.fileName = "weather_files.txt"
	}
  Calendar calendar {
    .initialDateTime = 1/5/2009
  }
  Records weather {
    .fileName = scenario[FileName]
  }
	Box butterfly {
		Stage egg {
			.initial = 100 
			.duration = 140
      .timeStep = ./time[step]
			DayDegrees time {
        .T0 = 5
        .T = weather[Tavg]
      }
		}
		Stage larva {
			.inflow = ../egg[outflow]
			.duration = 200
      .timeStep = ./time[step]
			DayDegrees time {
        .T0 = 8
        .T = weather[Tavg]
      }
		}
		Stage pupa {
			.inflow = ../larva[outflow]
			.duration = 100
      .timeStep = ./time[step]
			DayDegrees time {
        .T0 = 10
        .T = weather[Tavg]
      }
		}
    Stage adult {
      .inflow = ../pupa[outflow]
      .duration = 28
      .timeStep = 1
    }
	}
	OutputR {
    .end = "butterfly_analysis.R"
    .popUp = TRUE
    PageR {
      .xAxis = calendar[date]
      PlotR {
        .ports = (*[content])
        .end = "scripts/month_breaks.R"
      }
      PlotR {
//        .hide = TRUE
        .ports = (*[outflowTotal])
      }
		}
	}
}
