// phys-dev-appl-6.box
Simulation sim {
  .steps = 60
  Calendar calendar {
  }
  //
  // Host
  //
  Box host {
    +juvenileDuration = 10
    +adultDuration = 30
    +juvenileInfectionRate = 0.04 // Should be dynamic
    +adultInfectionRate    = 0.01 // Should be dynamic
    Box S {
      Stage juvenile {
        .initial = 80
        .duration = host[juvenileDuration]
        .phaseOutflowProportion = host[juvenileInfectionRate]
      }
      Stage adult {
        .initial = 50
        .inflow = ../juvenile[outflow]
        .duration = host[adultDuration]
        .phaseOutflowProportion = host[adultInfectionRate]
      }
      Stage reproduction {
        .initial = ../adult[initial]
        .inflow|sum = host/*/juvenile[outflowSum]
        .duration = 20
        .growthFactor = 2.5
      }
    }
    Box E {
      StageAndPhase juvenile {
        .duration = host[juvenileDuration]
        .phaseInflow = host/S/juvenile[phaseOutflow]
        .phaseDuration = 10
      }      
      StageAndPhase adult {
        .inflow = ../juvenile[outflow]
        .duration = host[adultDuration]
        .phaseInflow = host/S/adult[phaseOutflow]
        .phaseDuration = 10
      }
    }
    Box I {
      StageAndPhase juvenile {
        .duration = host[juvenileDuration]
        .phaseInflow = host/E/juvenile[phaseOutflow]
        .phaseDuration = 20
      }      
      StageAndPhase adult {
        .inflow = ../juvenile[outflow]
        .duration = host[adultDuration]
        .phaseInflow = host/E/adult[phaseOutflow]
        .phaseDuration = 20
      }
    }
    Box R {
      Stage juvenile {
        .duration = host[juvenileDuration]
        .phaseInflow = host/I/juvenile[phaseOutflow]
      }
      Stage adult {
        .inflow = ../juvenile[outflow]
        .duration = host[adultDuration]
        .phaseInflow = host/I/adult[phaseOutflow]
      }
    }
  }
  //
  // Vector
  //
  Box vector {
    +offspringDuration = 7
    +juvenileDuration = 14
    +adultDuration = 14
    +fecundity = 5
    +infectionRate = 0.04
    Box S {
      Stage offspring {
        .initial = 100
        .duration = vector[offspringDuration]
      }
      Stage juvenile {
        .inflow = ../offspring[outflow]
        .duration = vector[juvenileDuration]
      }
      Box adult {
        Stage individuals {
          .inflow = ../../juvenile[outflow]
          .duration = vector[adultDuration]
          .phaseOutflowProportion = vector[infectionRate]
        }
        Stage reproduction {
          .inflow = ../../juvenile[outflow]
          .duration = vector[adultDuration]
          .phaseOutflowProportion = vector[infectionRate]
          .growthFactor = vector[fecundity]
        }
      }
    }
    Box E {
      +phaseDuration = 3
      Box adult {
        StageAndPhase individuals {
          .duration = vector[adultDuration]
          .phaseInflow = vector/S/adult/individuals[phaseOutflow]
          .phaseDuration = vector/E[phaseDuration]
        }
        StageAndPhase reproduction {
          .duration = vector[adultDuration]
          .growthFactor = vector[fecundity]
          .phaseInflow = vector/S/adult/reproduction[phaseOutflow]
          .phaseDuration = vector/E[phaseDuration]
        }
      }
    }
    Box I {
      Stage offspring {
        // .inflow = ../adult/fecundity[outflow]
        .duration = vector[offspringDuration]
      }
      Stage juvenile {
        .inflow = ../offspring[outflow]
        .duration = vector[juvenileDuration]
      }
      Box adult {
        Stage individuals {
          .inflow = ../../juvenile[outflow]
          .duration = vector[adultDuration]
          .phaseInflow = vector/E/adult/individuals[phaseOutflow]
        }
        Stage reproduction {
          .inflow = ../../juvenile[outflow]
          .duration = vector[adultDuration]
          .growthFactor = vector[fecundity]
          .phaseInflow = vector/E/adult/reproduction[phaseOutflow]
        }
      }
    }
  }
  OutputR output {
    .end = "phys-dev-appl-6-end.R"
    Box initial {
      +initHostJuvenile = host/S/juvenile[initial]
      +initHostAdult = host/S/adult[initial]
      +initVectorOffspring = vector/S/offspring[initial]
    }
    Box fecundity {
      +hostFecundity = host/S/reproduction[growthFactor]
      +vectorFecundity = vector/S/adult/reproduction[growthFactor]
    }
    Box flows {
      +e = host/E/juvenile[phaseInflowTotal]
      +f = host/I/juvenile[phaseInflowTotal]
      +g = host/R/juvenile[phaseInflowTotal]
      +h = host/S/adult[inflowTotal]
      +i = host/E/adult[inflowTotal]
      +j = host/I/adult[inflowTotal]
      +k = host/R/adult[inflowTotal]
      +l = host/E/adult[phaseInflowTotal]
      +m = host/I/adult[phaseInflowTotal]
      +n = host/R/adult[phaseInflowTotal]
      +o = host/S/reproduction[outflowTotal]
      +p = host/S/adult[outflowTotal]
      +q = host/E/adult[outflowTotal]
      +r = host/I/adult[outflowTotal]
      +s = host/R/adult[outflowTotal]
      +e_ = vector/S/juvenile[inflowTotal]
      +f_ = vector/I/juvenile[inflowTotal]
      +g_ = vector/S/adult/individuals[inflowTotal]
      +h_ = vector/I/adult/individuals[inflowTotal]
      +i_ = vector/E/adult/individuals[phaseInflowTotal]
      +j_ = vector/E/adult/reproduction[phaseInflowTotal]
      +k_ = vector/I/adult/individuals[phaseInflowTotal]
      +l_ = vector/I/adult/reproduction[phaseInflowTotal]
      +m_ = vector/S/adult/individuals[outflowTotal]
      +n_ = vector/E/adult/individuals[outflowTotal]
      +o_ = vector/I/adult/individuals[outflowTotal]
      +p_ = vector/S/adult/reproduction[outflowTotal]
      +q_ = vector/E/adult/reproduction[outflowTotal]
      +r_ = vector/I/adult/reproduction[outflowTotal]
    }
    PageR {
      PlotR {
        .ncol = 4
        .ports = output/flows[*]
      }
    }
    PageR {
      .title = "Host"
      .ncol = 1
      PlotR {
        .layout = "merged"
        .ports = host/*/juvenile[content]
      }
      PlotR {
        .layout = "merged"
        .ports = host/*/adult[content]
      }
    }
    PageR {
      .title = "Vector"
      .ncol = 1
      PlotR {
        .layout = "merged"
        .ports = vector/*/offspring[content]
      }
      PlotR {
        .layout = "merged"
        .ports = vector/*/juvenile[content]
      }
      PlotR {
        .layout = "merged"
        .ports = vector/*/adult/individuals[content]
      }
    }
    OutputText {
        .ports = (output/initial[*] output/fecundity[*])
    }
  }
}