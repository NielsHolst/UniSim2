// sobol2.box
Simulation sim {
  .iterations = 30072
  .steps = 35
  .silent = TRUE
  Box random {
    RandomiserMonteCarlo randomiser {
    // RandomiserStratified randomiser {
    // RandomiserSobolSequence randomiser {
      .doSensitivityAnalysis = TRUE
    }
    RandomUniform egg_duration {
      .min = 5
      .max = 12
    }
    RandomUniformInt egg_k {
      .min = 1
      .max = 30
    }
    RandomUniform larva_duration {
      .min = 5
      .max = 25
    }
    RandomUniformInt larva_k {
      .min = 1
      .max = 30
    }
  }  
  Box butterfly {
    Stage egg {
      .initial = 100 
      .duration = random/egg_duration[value]
      .k = random/egg_k[value]
    }
    Stage larva {
      .inflow = ../egg[outflow]
      .duration = random/larva_duration[value]
      .k = random/larva_k[value]
    }
    Stage pupa {
      .inflow = ../larva[outflow]
      .duration = 7
    }
    Stage adult {
      .inflow = ../pupa[outflow]
      .duration = 30
    }
  }
  OutputR {
    PageR {
      .xAxis = random/*[value]
      PlotR {
        .ports = (larva[content]|end pupa[content]|end)
        .ggplot = "geom_point(size=2, alpha=0.3) + geom_smooth(colour='yellow',size=1) + theme_classic()"
      }
    }
  }
}

