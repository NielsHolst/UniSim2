// butterfly3b.box
Simulation sim {
  .steps = 365
  Calendar calendar {
    .initialDateTime = 1/1/2009  
  }
  Records weather {
    .fileName = "weather/flakkebjerg 2009.txt"
  }
  Box butterfly {
    Stage egg {
      .initial = 100 
      .inflow = ../adult/oviposition[outflow]  
      .duration = 140
      .timeStep = ./time[value] 
      OnOff time { 
        .x = calendar[dayOfYear]
        .xOn = 107    // 17 April
        .xOff = 152   //  1 June
        .valueOn = ./dayDegrees[step]
        .valueOff = 0
        DayDegrees dayDegrees {
          .T0 = 5
          .T = weather[Tavg]
        }
      }
    }
    Stage larva {
      .inflow = ../egg[outflow]
      .duration = 200
      .timeStep = ./time[step]
      DayDegrees time {
        .T0 = 8
        .T = weather[Tavg]
      }
    }
    Stage pupa {
      .inflow = ../larva[outflow]
      .duration = 100
      .timeStep = ./time[step]
      DayDegrees time {
        .T0 = 10
        .T = weather[Tavg]
      }
    }
    Box adult {
      Stage adult {
        .inflow = ../../pupa[outflow]
        .duration = 28
        .timeStep = 1
      }
      Stage preOviposition {
        .inflow = ../../pupa[outflow]
        .duration = 5
        .timeStep = 1
      }
      Stage oviposition {
        .inflow = ../preOviposition[outflow]
        .duration = 10
        .timeStep = 1
        .growthFactor = 40
      }
    }
  }
  OutputR {
    PageR {
      .xAxis = calendar[date]
      .ncol = 3 
      +monthBreaks = "geom_line(size=1.1) + 
                      scale_x_datetime(
                        breaks = date_breaks('3 months'), 
                        labels = date_format('%b')
                      ) + 
                      ylim(0,NA)" //!
      PlotR {
        .ports = *[content]
        .ggplot = ..[monthBreaks]
        .transform = "log10" //!
      }
      PlotR {
        .ports = *[outflowTotal]
        .ggplot = ..[monthBreaks]
        .transform = "log10" //!
      }
      PlotR { 
        .ports = (calendar[dayOfYear] weather[Tavg] egg/time/dayDegrees[step] egg/time[isOn] egg[timeStep])
        .ggplot = ..[monthBreaks]
      }
    }
  }
}

